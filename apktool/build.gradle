plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Configure each application variant
android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach { output ->
        // Format the current date
        def formattedDate = new Date().format('yyyyMMddHHmmss')
        // Get the version name and build type
        def versionName = variant.versionName
        def buildType = variant.buildType.name
        // Create a new APK name
        def newApkName = "apktool-${versionName}-${buildType}-${formattedDate}.apk"
        // Set the output file name
        output.outputFileName = newApkName
    }
}

android {
    namespace 'com.a4455jkjh.apktool'
    compileSdk 32

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    defaultConfig {
        applicationId "com.a4455jkjh.apktool"
        minSdk 14
        targetSdk 32
        versionCode 1903280240
        versionName "2.4.0-190328"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Fix Duplicate class org.intellij.lang.annotations.Identifier
    configurations {
        implementation.exclude group: 'org.jetbrains', module: 'annotations'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.drawerlayout:drawerlayout:1.0.0"
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.interpolator:interpolator:1.0.0'
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
}
